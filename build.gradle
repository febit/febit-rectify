//buildscript {
//  configurations.all { resolutionStrategy.cacheChangingModulesFor 0, 'seconds' }
//}
plugins {
  alias libs.plugins.febit.java
  alias libs.plugins.febit.maven.publish
  alias libs.plugins.febit.codegen.module
  alias libs.plugins.license
}

group 'org.febit.rectify'
version '2.0.0-SNAPSHOT'

allprojects {
  apply plugin: 'com.github.hierynomus.license'

  group rootProject.group
  version rootProject.version

  repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
  }

  standardMavenPublish {
    pom {
      customPom(it)
    }
  }

  codegenModule {
    defaultTemplate = fromFile("$rootDir/etc/module.java.tmpl")
  }


  license {
    mapping {
      java = 'SLASHSTAR_STYLE'
    }
    include '**/*.java'
    strictCheck false
    skipExistingHeaders false
    header rootProject.file('etc/license-header.txt')
  }

}

configure(subprojects.findAll {
  !it.name.endsWith('-bom')
}) {
  apply plugin: 'java-library'

  java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    withSourcesJar()
  }

  tasks.named("licenseMain").configure {
    mustRunAfter tasks.named("generateModule")
  }

  dependencyManagement {
    resolutionStrategy {
      cacheChangingModulesFor 0, 'seconds'
    }
    imports {
      mavenBom "${libs.febit.commons.deps.get()}"
    }
    dependencies {
      dependencySet(group: 'org.apache.flink', version: "${libs.versions.flink.get()}") {
        entry "flink-clients"
        entry "flink-format-common"
        entry "flink-streaming-java"
        entry "flink-table"
        entry "flink-table-api-java"
        entry "flink-table-common"
      }
      dependency "${libs.wit.core.get()}"
      dependency "${libs.calcite.core.get()}"
      dependency "${libs.avro.get()}"
      dependency "${libs.sqlline.get()}"
    }
  }

  test {
    useJUnitPlatform()
  }

  dependencies {
    compileOnly 'org.jspecify:jspecify'
    compileOnly 'com.github.spotbugs:spotbugs-annotations'
    testCompileOnly 'org.jspecify:jspecify'
    testCompileOnly 'com.github.spotbugs:spotbugs-annotations'

    testImplementation 'org.febit:febit-commons-test'
    testRuntimeOnly 'org.slf4j:slf4j-simple'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  }
}

def customPom(MavenPom pom) {
  pom.with {
    url = 'https://github.com/febit/febit-rectify'
    organization {
      name = 'Febit'
      url = 'https://github.com/febit'
    }
    licenses {
      license {
        name = 'Apache License, Version 2.0'
        url = 'https://github.com/febit/febit-rectify/blob/master/LICENSE.txt'
        distribution = 'repo'
      }
    }
    scm {
      url = 'https://github.com/febit/febit-rectify'
      connection = 'scm:git:https://github.com/febit/febit-rectify.git'
      developerConnection = 'scm:git:https://github.com/febit/febit-rectify.git'
    }
    issueManagement {
      system = 'GitHub'
      url = 'https://github.com/febit/febit-rectify/issues'
    }
    developers {
      developer {
        id = 'kurt'
        name = 'Kurt Zhu'
        email = 'zqq@febit.org'
        timezone = '+8'
      }
    }
  }
}
